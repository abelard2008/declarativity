program scheduler;

import java.lang.String;
import java.lang.Integer;
import java.lang.Float;
import java.lang.System;
import jol.types.basic.ValueList;
import org.apache.hadoop.mapred.declarative.Constants.JobState;
import org.apache.hadoop.mapred.declarative.Constants.TaskState;
import org.apache.hadoop.mapred.declarative.Constants.TaskPhase;
import org.apache.hadoop.mapred.declarative.Constants.TaskType;
import org.apache.hadoop.mapred.TaskTrackerAction;
import org.apache.hadoop.mapred.declarative.util.Function;
import org.apache.hadoop.mapred.JobPriority;


/*************** Task Attempt Scheduler *********************/

define(taskAttempts, keys(0,1), {JobID, TaskID, Integer});
define(schedule,      {JobID, TaskID, String});
define(mapSlots,      {JobID, String, ValueList});
define(reduceSlots,   {JobID, String, ValueList});

public
taskAttempts(JobId, TaskId, count<AttemptId>) :-
	jobtracker::taskAttempt(JobId, TaskId, AttemptId);

jobtracker::taskAttempt(JobId, TaskId, AttemptID, 0, State, Phase, null, TrackerName, null, 0L, 0L) :-
	schedule(JobId, TaskId, TrackerName),
	notin taskAttempts(JobId, TaskId, _),
	State := TaskState.RUNNING,
	Phase := TaskPhase.STARTING,
	AttemptID := 0;

jobtracker::taskAttempt(JobId, TaskId, AttemptID, 0, State, Phase, null, TrackerName, null, 0L, 0L) :-
	schedule(JobId, TaskId, TrackerName),
	taskAttempts(JobId, TaskId, Attempts),
	State := TaskState.RUNNING,
	Phase := TaskPhase.STARTING,
	AttemptID := Attempts;
	
	
actionSchedule
jobtracker::taskTrackerAction(TrackerName, TaskTrackerAction.ActionType.LAUNCH_TASK, Action) :-	
	jobtracker::taskAttempt#insert(JobId, TaskId, AttemptID, _, _, Constants.TaskPhase.STARTING, _, TrackerName, _, _, _),
	jobtracker::job(JobId, JobName, JobFile),
	jobtracker::task(JobId, TaskId, Type, Partition, Split, MapCount, _),
	Action := Type == Constants.TaskType.MAP ? 
	          Function.launchMap(Split, JobFile, TaskId, AttemptID, Partition) :
	          Function.launchReduce(JobFile, TaskId, AttemptID, Partition, MapCount);
 
/******************** Process Jobs *******************/
define(jobQueue,                keys(0), {JobID, JobPriority, Long, TaskType});
define(jobMapTasks,             keys(0), {JobID, Integer});
define(jobFinishedMapTasks,     keys(0), {JobID, Integer});
define(jobReduceTasks,          keys(0), {JobID, Integer});
define(jobFinishedReduceTasks,  keys(0), {JobID, Integer});

/* Put new jobs in a wait queue in a MAP phase. */
public
jobQueue(JobId, Priority, SubmitTime, TaskType.MAP) :-
	jobtracker::job#insert(JobId, JobName, JobFile, JobConf, User, URL, Priority, SubmitTime, Status),
	Status.state() == JobState.PREP;
	
public delete
jobQueue(JobId, Priority, SubmitTime, Phase) :-
	jobtracker::job(JobId, JobName, JobFile, JobConf, User, URL, Priority, SubmitTime, Status),
	jobQueue(JobId, Priority, SubmitTime, Phase),
	Status.state() == JobState.SUCCEEDED || Status.state() == JobState.FAILED;
	
/* Switch to reduce phase when all maps finish. */
jobQueue(JobId, Priority, SubmitTime, TaskType.REDUCE) :-
	jobFinishedMapTasks#insert(JobId, Maps), jobMapTasks(JobId, Maps),
	jobQueue(JobId, Priority, SubmitTime, Phase),
	Phase == TaskType.MAP;
	
public
jobMapTasks(JobId, count<TaskId>) :-
	jobtracker::task(JobId, TaskId, Type, _, _, _, _),
	Type == TaskType.MAP;
	
public
jobFinishedMapTasks(JobId, count<TaskId>) :-
	jobtracker::task(JobId, TaskId, Type, _, _, _, Status),
	Type == TaskType.MAP, Status.state() == TaskState.SUCCEEDED;
	
/******************** Task Statistics *******************/
/* Task Progress stores the min, max, avg progress for a jobs tasks. */
define(taskProgress, keys(0,1), {JobID, TaskType, Float, Float, Float});

public
taskProgress(JobId, Type, min<Progress>, max<Progress>, avg<Progress>) :-
	jobtracker::task#insert(JobId, TaskId, Type, _, _, _, Status),
	Status.state() == TaskState.RUNNING || Status.state() == TaskState.SUCCEEDED, 
	Progress := Status.progress();

/******************** Schedule Tasks *******************/
	
define(trackerSlots, keys(0), {String, String, Long, Integer, Integer});
define(candidate,    keys(),  {JobID, TaskID, TaskType, String, Long});
define(mapProposal,    {JobID, String, ValueList});
define(reduceProposal, {JobID, String, ValueList});

/* Schedule task on tracker from flattened priorities. */
mapSlots(JobId, TrackerName, limit<TaskId, Maps>) :-
	flatten(mapProposal(JobId, TrackerName, Priorities)),
	candidate(JobId, TaskId, Type, TrackerName, Priority),
	trackerSlots(TrackerName, Host, _, Maps, Reduces),
	Priority == (Long) Priorities;
	
reduceSlots(JobId, TrackerName, limit<TaskId, Reduces>) :-
	flatten(reduceProposal(JobId, TrackerName, Priorities)),
	candidate(JobId, TaskId, Type, TrackerName, Priority),
	trackerSlots(TrackerName, Host, _, Maps, Reduces),
	Priority == (Long) Priorities;
	
schedule(JobId, TaskId, TrackerName) :-
	flatten(mapSlots(JobId, TrackerName, Tasks)),
	TaskId := (TaskID) Tasks;
	
schedule(JobId, TaskId, TrackerName) :-
	flatten(reduceSlots(JobId, TrackerName, Tasks)),
	TaskId := (TaskID) Tasks;
	
/* clean up scheduled candidates. */
delete
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	schedule(JobId, TaskId, TrackerName),
	candidate(JobId, TaskId, Type, TrackerName, Priority);
	
/* Update tracker slots taken directly from tracker heartbeat. */
public
trackerSlots(TrackerName, Host, Timestamp, MaxMap - MapCount, MaxReduce - ReduceCount) :-
	jobtracker::taskTracker(TrackerName, Host, HttpPort, Timestamp, Failures, MapCount, ReduceCount, MaxMap, MaxReduce);

/* Priority 1: Schedule unassigned tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	trackerSlots(TrackerName, Host, _, Maps, Reduces),
	jobQueue(JobId, JPriority, SubmitTime, Type),
	jobtracker::task(JobId, TaskId, Type, _, _, _, Status),
    notin jobtracker::taskAttempt(JobId, TaskId, _, _, _, _, _, _, _, _, _),
	Type == TaskType.MAP && Maps > 0 || Type == TaskType.REDUCE && Reduces > 0,
	Priority := Function.priority(1, JPriority, SubmitTime);
	
/* Priority 2: Schedule failed tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	trackerSlots(TrackerName, Host, _, Maps, Reduces),
	jobQueue(JobId, JPriority, SubmitTime, Type),
	jobtracker::task(JobId, TaskId, Type, _, _, _, Status),
	Status.state() == TaskState.FAILED,
	Status.attempts() < 3,
    notin jobtracker::taskAttempt(JobId, TaskId, _, _, TaskState.FAILED, _, _, TrackerName, _, _, _),
	Type == TaskType.MAP && Maps > 0 || Type == TaskType.REDUCE && Reduces > 0,
	Priority := Function.priority(2, JPriority, SubmitTime);
	
/* Priority 3: Speculative task execution on the min priority job. */
/*
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	trackerSlots(TrackerName, Host, _, Maps, Reduces),
	jobQueue(JobId, JPriority, JobSubmitTime, Type),
	jobtracker::task(JobId, TaskId, Type, _, _, _, Status),
	taskProgress(JobId, Type, MinProgress, MaxProgress, AvgProgress),
    notin jobtracker::taskAttempt(JobId, TaskId, _, _, TaskState.FAILED, _, _, TrackerName, _, _, _),
	Status.state() == TaskState.RUNNING,
	Type == TaskType.MAP && Maps > 0 || Type == TaskType.REDUCE && Reduces > 0,
	FOO := java.lang.System.currentTimeMillis(),
	(java.lang.System.currentTimeMillis() - JobSubmitTime) > 60000L,
	Status.progress() < AvgProgress - 0.2,
	Priority := Function.priority(3, JPriority, SubmitTime);
	*/
	
	
/******************** Schedule Map Tasks *******************/
define(bestMapDistance, {JobID, TaskID, String, Integer, Integer, Integer});

/* Propose the map tasks. */
mapProposal(JobId, TrackerName, bottomk<Priority,MapSlots>) :-
	candidate(JobId, TaskId, Type, TrackerName, Priority),
	trackerSlots(TrackerName, Host, _, MapSlots, ReduceSlots),
	Type == TaskType.MAP, MapSlots > 0;
	
/******************** Schedule Reduce Tasks *******************/

reduceProposal(JobId, TrackerName, bottomk<Priority,ReduceSlots>) :-
	candidate(JobId, TaskId, Type, TrackerName, Priority),
	trackerSlots(TrackerName, Host, _, MapSlots, ReduceSlots),
	Type == TaskType.REDUCE, ReduceSlots > 0;
	
	
	
	