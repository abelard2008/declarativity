program policy;

import java.util.Set;
import jol.types.basic.ValueList;
import org.apache.hadoop.mapred.declarative.Constants;
import org.apache.hadoop.mapred.declarative.util.Function;
import org.apache.hadoop.mapred.declarative.util.FileInput;
import org.apache.hadoop.mapred.JobID;
import org.apache.hadoop.mapred.TaskID;

define(candidate,      {JobID, TaskID, Constants.TaskType, String, ValueList});
define(mapSlots,       {String, Integer, ValueList, Constants.TaskType, Set});
define(reduceSlots,    {String, Integer, ValueList, Constants.TaskType, Set});
define(scheduleTask,   {String, TaskID});

/******************** Task Statistics *******************/
/* Task Progress stores the min, max, avg progress for a jobs tasks. */
define(taskProgress, keys(0,1), {JobID, Constants.TaskType, Float, Float, Float});

/*
public
taskProgress(JobId, Type, min<Progress>, max<Progress>, avg<Progress>) :-
	jobtracker::task#insert(JobId, TaskId, Type, _, _, _, Status),
	Status.state() == Constants.TaskState.RUNNING || Status.state() == Constants.TaskState.SUCCEEDED, 
	Progress := Status.progress();
*/
	
/******************** Signal to schedule a task *******************/
	
scheduler::schedule(JobId, TaskId, TrackerName) :-
	scheduleTask(TrackerName, TaskId),
	jobtracker::task(JobId, TaskId, _, _, _, _, _);

scheduleTask(TrackerName, TaskId) :-
	assignTracker(mapSlots(TrackerName, Maps, Priority, Type, Tasks)),
	TaskId := (TaskID) Tasks;
	
scheduleTask(TrackerName, TaskId) :-
	assignTracker(reduceSlots(TrackerName, Reduces, Priority, Type, Tasks)),
	TaskId := (TaskID) Tasks;
	
/******************** Schedule Map Tasks *******************/

/* Schedule task on tracker from flattened priorities. */
public
mapSlots(TrackerName, Maps, Priority, Type, set<TaskId>) :-
	bestCandidate(candidate(JobId, TaskId, Type, TrackerName, Priority)),
	jobtracker::taskTracker(TrackerName, _, _, _, _, _,  MapCount, ReduceCount, MaxMap, MaxReduce),
	Type == Constants.TaskType.MAP, MaxMap - MapCount > 0
    {
        Maps := 2 * (MaxMap - MapCount);
    };
	
/******************** Schedule Reduce Tasks *******************/
	
public
reduceSlots(TrackerName, Reduces, Priority, Type, set<TaskId>) :-
	bestCandidate(candidate(JobId, TaskId, Type, TrackerName, Priority)),
	jobtracker::taskTracker(TrackerName, _, _, _, _, _,  MapCount, ReduceCount, MaxMap, MaxReduce),
	Type == Constants.TaskType.REDUCE, MaxReduce - ReduceCount > 0
    {
        Reduces := 2 * (MaxReduce - ReduceCount);
    };
	
/**************************************************************/

/* Priority 1: Schedule unassigned map tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	scheduler::trackerSlots(TrackerName, Host, _, Maps, Reduces),
	jobtracker::taskFileLocation(JobId, TaskId, FileLocation),
    jobtracker::job(JobId, _, _, _, _, _, JPriority, SubmitTime, _, JobStatus),
	jobtracker::task(JobId, TaskId, Type, _, _, MapCount, Status),
    jobtracker::tasksWaitingForSchedule(WaitingTaskCount),
    WaitingTaskCount < 500 || Host == FileLocation || Function.coinFlip((3 * Maps).floatValue() / WaitingTaskCount.floatValue()),
	Status.state() == Constants.TaskState.UNASSIGNED,
	Type == Constants.TaskType.MAP, Maps > 0
	{
		LocalFile := Host == FileLocation ? 0 : 1;
		Priority := [JPriority, SubmitTime, 1, LocalFile];
	};

/* Priority 1: Schedule unassigned reduce tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	scheduler::trackerSlots(TrackerName, Host, _, Maps, Reduces),
    jobtracker::job(JobId, _, _, _, _, _, JPriority, SubmitTime, _, JobStatus),
    JobStatus.status().mapProgress() > 0f,
	jobtracker::task(JobId, TaskId, Type, _, _, _, Status),
	Status.state() == Constants.TaskState.UNASSIGNED,
	Type == Constants.TaskType.REDUCE, Reduces > 0
	{
		Priority := [JPriority, SubmitTime, 1, 0];
	};
	
/* Priority 2: Schedule failed map tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	scheduler::trackerSlots(TrackerName, Host, _, Maps, Reduces),
    jobtracker::failedTasks(JobId, TaskId, Type, Status), Status.attempts() < 3, 
    jobtracker::job(JobId, _, _, _, _, _, JPriority, SubmitTime, _, JobStatus),
	jobtracker::taskFileLocation(JobId, TaskId, FileLocation),
	notin jobtracker::taskAttempt(JobId, TaskId, _, _, Constants.TaskState.FAILED, _, _, TrackerName, _, _, _,_,_)
	{
		LocalFile := Host == FileLocation ? 0 : 1;
		Priority := [JPriority, SubmitTime, 2, LocalFile];
	};

/* Priority 2: Schedule failed reduce tasks. */
public
candidate(JobId, TaskId, Type, TrackerName, Priority) :-
	scheduler::trackerSlots(TrackerName, Host, _, Maps, Reduces),
    jobtracker::failedTasks(JobId, TaskId, Type, Status), Status.attempts() < 3, 
    jobtracker::job(JobId, _, _, _, _, _, JPriority, SubmitTime, _, JobStatus),
	notin jobtracker::taskAttempt(JobId, TaskId, _, _, Constants.TaskState.FAILED, _, _, TrackerName, _, _, _,_,_)
	{
		Priority := [JPriority, SubmitTime, 2, 0];
	};
	
