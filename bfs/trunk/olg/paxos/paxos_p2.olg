program multipaxos;
	
import java.lang.System;
import jol.core.Runtime;

//timer(periodic,logical,1,1,1);
	
define(acceptRequest,keys(0,1),{String,Integer, Integer,String,Integer});
//define(sendBeginRound,keys(0,1,2),{String, Integer,Integer,String,String});
define(sendBeginRound,{String, Integer,Integer,String,String});
//define(stasis_accept,keys(0,1),{String, Integer,Integer,String,String});

define(stasis_accept,{String, Integer,Integer,String,String});
define(sendAccept,keys(0,1,4),{String, Integer,Integer,String,String});
define(decree,keys(0,1),{String, Integer,Integer,String});
define(stasis_success,keys(0,1),{String,Integer, Integer,String,String});
	
define(acceptCnt,keys(0,1),{String,Integer,Integer,Integer});
	
define(tick,keys(0,1),{String,String,Integer});


/**************************************************************************************
3.  After receiving a LastVote (b, v) message from every priest in some majority
set Q, where b = lastTried [p], priest p initiates a new ballot with number b,
quorum Q, and decree d, where d is chosen to satisfy B3. He then sends a
BeginBallot (b, d) message to every priest in Q.
***************************************************************************************/


public
r3_b
acceptRequest(Master,Instance,Round,OldDecree,MaxB) :- 
  paxos::quorum(Master,Round),
	paxos::maxPrevRound(Master,Instance,MaxB),
	paxos::prepare(Master,Round,Decree),
  paxos::instance(Master,I),
  I >= Instance,
	paxos::sendPromise(Master,Instance,Round,MaxB,OldDecree,Agent),
	MaxB != -1;

public
r3_c	
acceptRequest(Master,Instance,Round,Decree,-1) :- 
  paxos::quorum(Master,Round),
	paxos::maxPrevRound(Master,Instance,MaxB),
  paxos::instance(Master,Instance),
	paxos::sendPromise(Master,Instance,Round,MaxB,OldDecree,Agent),
	MaxB == -1,
	paxos::prepare(Master,Round,Decree);
	
// MULTIPAXOS!

//watch(storedDecreeRequest,aed);

define(topOfQueue_p2, keys(0), {String, Long});
watch(topOfQueue_p2, ae);

public 
topOfQueue_p2(A, min<Id>) :-
  paxos::storedDecreeRequest(A, B, C, Id);


watch(acceptRequest, ae);
public
r3_d
acceptRequest(Master,I,Round,Decree,-2) :-
  paxos::storedDecreeRequest(Master,Decree,From, Id),
  paxos::quorum(Master,Round),
  paxos::instance(Master,I),
  topOfQueue_p2#insert(Master, Id),
  paxos::priestCnt(Master, Pcnt),
  Pcnt > 1,
  ////tic(),
  notin acceptRequest(Master, I, _, _, _),
  notin stasis_success(Master,I,_,_,_),
  I > 0;

timer(tic, physical, 300, 300, infinity);

watch(paxos::topOfQueue, a);

public
r3_e
sendBeginRound(@Agent,Instance,Round,Decree,Master) :- 
  acceptRequest(@Master,Instance,Round,Decree,Kind),
	paxos::parliament(@Master,Agent);

/**************************************************************************************
r4. Upon receipt of a BeginBallot (b,d) message with b = nextBal [q], priest q casts
his vote in ballot number b, sets prevVote [q] to this vote, and sends a Voted (b, q)
message to p. (A BeginBallot (b, d) message is ignored if b = nextBal [q].)
***************************************************************************************/
public
r4_a
stasis_accept(Agent,Instance,Round,Decree,Master) :-
  sendBeginRound(Agent,Instance,Round,Decree,Master),
	paxos::nextBal(Agent,OldB),
  paxos::instance(Agent,Instance),
	Round == OldB;

/*
repair
stasis_accept(Agent,Instance,Round,Decree) :-
  sendBeginRound(Agent,Instance,Round,Decree,Master),
	nextBal(Agent,OldB),
  instance(Agent,I),
	Instance < I
  stasis_accept(Agent,Instance,Round,Decree)
*/

public
r4_b	
paxos::prevVote(Agent,Instance,Round,Decree) :- 
  paxos::prevVote(Agent,OI,Old,OD),
	paxos::stasis_lastPromise(Agent,IO2,Round,OldRound,OldDecree),
	stasis_accept#insert(Agent,Instance,Round,Decree),
	Round >= Old;

r4_c	
sendAccept(@Master,Instance,Round,Decree,Agent) :- 
  stasis_accept(@Agent,Instance,Round,Decree,Master);
	
votes
acceptCnt(Master,Instance,Round,count<Agent>) :- 
  sendAccept(Master,Instance,Round,Decree,Agent);


clean1
delete
sendAccept(Master,Instance,Round,Decree,Agent) :-
    sendAccept(Master, Instance, Round, Decree, Agent),
    stasis_success#insert(Master, Instance, Round, Decree);
watch(sendAccept, e);
    

/**************************************************************************************
r5. If p has received a Voted (b, q) message from every priest q in Q (the quorum
for ballot number b), where b = lastTried [p], then he writes d (the decree of
that ballot) in his ledger and sends a Success (d) message to every priest.
***************************************************************************************/
public
r5_a	
decree(Master,Instance,Round,Decree) :- 
  paxos::lastTried(Master,Round),
	acceptCnt#insert(Master,Instance,Round,Votes),
  paxos::priestCnt(Master,Priests),
  Votes > (Priests / 2), 
	acceptRequest(Master,Instance,Round,Decree);


watch(stasis_success, ae);

/*
public
r5_b	
stasis_success(@Agent,Instance,Round,Decree,From) :-
  decree(@Master,Instance,Round,Decree),
	paxos::storedDecreeRequest(@Master,Decree,From,_),
	// we interpret "every" to mean a broadcast.
	paxos::parliament(@Master,Agent);
*/
public
r5_b	
stasis_success(Agent,Instance,Round,Decree,From) :-
  stasis_accept#insert(Agent, Instance, Round, Decree, From),
  Agent != From;


public
r5_b2
stasis_success(Master, Instance, Round, Decree, From) :-
    decree#insert(Master,Instance,Round,Decree),
	paxos::storedDecreeRequest(Master,Decree,From,_);


// breakout.
public
r5_d
stasis_success(Agent,Instance,Round,Decree,From) :-
  paxos::storedDecreeRequest(Agent,Decree,From, Id),
  paxos::priestCnt(Agent,1),
  Round := -1,
  paxos::instance(Agent,Instance),
  topOfQueue_p2#insert(Master, Id);


watch(decree,ae);
watch(acceptCnt, ae);

// a periodic event for fire reads on stasis tables at startup.
timer(stasis_wakeup, logical, 10, 10, 1);
	
// any learner can reply to the client, or set-wise, they all can...
public
response
paxos_global::requestStatus(Master,From,Decree,Instance,"passed") :- 
  // a bare minimum first recovery rewrite
  stasis_wakeup(), 
  stasis_success(Master,Instance,Round,Decree,From);




public
arrr
delete
paxos::quorum(Master,Round) :-
  paxos::quorum(Master,Round),
  sendBeginRound(Master,Instance,NewRound,Decree,Other),
  Master != Other,
  NewRound > Round;

define(localized_wakeup, keys(0), {String,Integer});
localized_wakeup(Id, 42) :- multipaxos::stasis_wakeup(), paxos_global::members(Id,_);

/********************************************************************
 r6 (?) on startup, play "catch-up"
********************************************************************/
stasis_success(@Agent,Instance,Round,Decree,From) :-
   stasis_success(@Peer,Instance,Round,Decree,From),
   paxos::parliament(@Agent,Peer),
   localized_wakeup#insert(@Agent,_);
/* Sync the other way too, just in case we're the only non-failed node with a record of the decree. */
stasis_success(@Peer,Instance,Round,Decree,From) :-
   stasis_success(@Agent,Instance,Round,Decree,From),
   paxos::parliament(@Agent,Peer),
   localized_wakeup#insert(@Agent,_);

/*
watch(paxos::instance,ae);
watch(paxos::sendPrepare,ae);
watch(paxos::nextBal,ae);
watch(paxos::sendPromise,ae);
watch(decree,ae);
watch(acceptCnt,ae);
watch(sendAccept,ae);
watch(sendBeginRound,ae);
watch(stasis_accept,ae);
watch(success,ae);
//watch(decreeRequest,ae);
	
watch(paxos_global::requestStatus,ae);	
//watch(maxPrevRound,ae);
watch(acceptRequest,ae);
//watch(receivedPromise,ae);
	
//watch(prevVote,ae);	
//watch(instance,ae);

//watch(quorum,ae);
watch(decree,ae);
watch(sendAccept,ae);
watch(success,ae);

watch(success,ae);
watch(paxos::quorum,ae);
watch(paxos::sendPromise,ae);
*/
