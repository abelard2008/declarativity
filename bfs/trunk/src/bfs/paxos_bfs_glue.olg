program glue;

import java.util.ArrayList;
import java.util.Arrays;

watch(bfs::file,ae);
watch(bfs::fchunk,ae);
watch(bfs::fpath,ae);
watch(bfs::fsize,ae);

public
paxos_global::decreeRequest(Master, Decree, From) :-
    paxos_global::id(Master),
    // err
    From := Master,
    bfs::create_request_pending(Master, Id, ParentId, FName, IsDir),
    DirStr := (IsDir ? ",d" : ",f"),
    Decree := FName + DirStr + ",i," + Id.toString() + "," + ParentId.toString();

public
paxos_global::decreeRequest(Master, Decree, From) :-
    paxos_global::id(Master),
    From := Master,
    bfs::rm_request_pending(Master, Id, FPath),
    DirStr := ",f",
    Decree := FPath + DirStr + ",d," + Id.toString() + ",-1";

// If we are adding a chunk to a file's chunklist, we likewise need to
// pass it through paxos. (and indeed, should block the requestor, but
// this is not implemented yet XXX)
public
paxos_global::decreeRequest(Master, Decree, From) :-
    paxos_global::id(Master),
    From := Master,
    bfs_chunks::candidate_datanode#insert(Master, Id, FPath, _),
    Decree := FPath + ",f,u," + Id.toString() + ",-1";

define(appView,keys(0,1,2),{String,Integer,String,Boolean,Integer,String,Integer});
watch(appView,ae);
public
appView(Master, Instance, Name, IsDir, ParentId, Op, Id) :-
    paxos_global::requestStatus#insert(Master, _, Decree, Instance, "passed"),
    NVL := Decree.split(","),
    Name := NVL[0],
    DirStr := NVL[1],
    Op := NVL[2],
    Id := Integer.valueOf(NVL[3]),
    ParentId := Integer.valueOf(NVL[4]),
    IsDir := (DirStr.equals("d") ? true : false);

// An insert op in the log induces a new metadata entry for a file. We use
// the Paxos instance # as the file ID, since that will be unique and consistent
// among all the masters.
public
bfs::file(Master, FileId, ParentId, FName, IsDir) :-
    appView#insert(Master, Instance, FName, IsDir, ParentId, Op, _),
    Op == "i",
    FileId := Instance;

// Add a new chunk to the file. Use the Paxos instance # as the chunk ID.
public
bfs::fchunk(Master, ChunkId, FileId) :-
    bfs::fpath(Master, FPath, FileId),
    bfs::file(Master, FileId, _, _, false),
    appView#insert(Master, Instance, FPath, _, _, Op, _),
    Op == "u",
    ChunkId := Instance;

public
bfs::newchunk_request_done(Master, Id, NewChunkId, NodeSet) :-
    bfs_chunks::candidate_datanode(Master, Id, _, NodeSet),
    appView(Master, Instance, _, _, _, "u", Id),
    NewChunkId := Instance;


// Delete the file
// XXX: We should use FileId, not FPath
public
delete
bfs::file(Master, FileId, FParent, FName, IsDir) :-
    appView#insert(Master, _, FPath, _, _, "d", Id),
    bfs::fpath(Master, FPath, FileId),
    bfs::file(Master, FileId, FParent, FName, IsDir);


// Use the "file" relation to construct the "fpath" relation. Note
// that these rules do NOT belong here, but we need them to be in
// the same program that deletes from bfs::file to workaround a
// JOL bug.

// Base case: root directory
public
bfs::fpath(@Master, FName, FileId) :-
    bfs::file(@Master, FileId, null, FName, true),
    FName == "/";

public
bfs::fpath(@Master, Path, FileId) :-
    bfs::file(@Master, FileId, FParent, FName, _),
    bfs::fpath(@Master, ParentPath, FParent),
    PathSep := (ParentPath == "/" ? "" : "/"),
    Path := ParentPath + PathSep + FName;

// Directories are considered to have size 0
public
bfs::fsize(@Master, FileId, FSize) :-
    bfs::file(@Master, FileId, _, _, true),
    FSize := 0L;

// Files with no chunks have size 0
public
bfs::fsize(@Master, FileId, FSize) :-
    bfs::file(@Master, FileId, _, _, false),
    notin compute_file_size(@Master, FileId, _),
    FSize := 0L;

public
bfs::fsize(@Master, FileId, FSize) :-
    bfs::file(@Master, FileId, _, _, false),
    compute_file_size(@Master, FileId, FSizeF),
    FSize := FSizeF.longValue();

define(compute_file_size, keys(0, 1), {String, Integer, Float});
public
compute_file_size(@Master, FileId, sum<LongChunkLen>) :-
    bfs::fchunk(@Master, ChunkId, FileId),
    bfs_heartbeat::chunks(@Master, _, ChunkId, ChunkLen, _),
    LongChunkLen := new Long(ChunkLen.longValue());


watch(bfs::create_request_done,aeid);

public
green
bfs::create_request_done(Master, Id, true) :-
    bfs::create_request_pending(Master, Id, ParentId, FName, _),
    appView(Master, _, FName, _, ParentId, "i", Id);

public
red1
bfs::create_request_done(Master, Id, false) :-
    bfs::create_request_pending(Master, Id, ParentId, FName, _),
    appView(Master, _, FName, _, ParentId, "i", Id2),
    Id != Id2;

public
bfs::rm_request_done(Master, Id) :-
    bfs::rm_request_pending(Master, Id, FPath),
    appView(Master, _, FPath, _, _, "d", Id);
